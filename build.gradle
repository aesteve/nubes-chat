plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'maven'
	id 'com.github.johnrengelman.shadow' version '1.2.2'
}

group='com.github.aesteve.nubes.examples'
version='0.1-SNAPSHOT'


repositories {
	mavenCentral()
	mavenLocal()
}

def NUBES_VERSION = '0.1-SNAPSHOT'
def VERTX_VERSION = '3.1.0'

dependencies {

	compile "com.github.aesteve:nubes-mongo:${NUBES_VERSION}" // Nubes as transitive dependency

	testCompile "io.vertx:vertx-unit:${VERTX_VERSION}"
	testCompile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.50.0'
	testCompile 'junit:junit:4.12'
}

compileJava {
	options.compilerArgs << '-parameters'
}

shadowJar  {
	classifier = ''
	manifest {
		attributes 'Main-Class': 'io.vertx.core.Starter'
		attributes 'Main-Verticle': 'java:com.github.aesteve.nubes.examples.chat.ChatServer'
	}
	mergeServiceFiles {
		include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
	}
	dependencies {
		exclude(dependency('io.vertx:codegen'))
		exclude(dependency('junit:junit'))
		exclude(dependency('org.mvel:mvel2'))
	}
	into 'web', {
		from 'web'
	}
}

task start(dependsOn: shadowJar) << {
	javaexec {
		main="-jar"
		args=[
			shadowJar.archivePath,
			"-conf",
			"conf.json"
		]
	}
}

test.outputs.upToDateWhen { false }

task wrapper(type: Wrapper) {
	gradleVersion = '2.8'
}
